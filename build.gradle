buildscript {
    ext {
        protobufVersion = '3.25.8'
        protobufPluginVersion = '0.9.6'
        grpcVersion = '1.79.0'
        springBootVersion = '4.0.3'
        springBootGrpcVersion = '3.1.0.RELEASE'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.jvm' version '2.3.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.10'
}

group = 'com.store'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation "net.devh:grpc-server-spring-boot-starter:${springBootGrpcVersion}"
    implementation "build.buf:protovalidate:0.2.1"

    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("io.specmatic.enterprise:executable:${specmaticEnterpriseVersion}")
    testImplementation "org.testcontainers:junit-jupiter:1.21.4"
}

sourceSets {
    main {
        proto {
            srcDir  'specmatic-order-contracts/io/specmatic/examples/store/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

test {
    dependsOn(generateProto)
    useJUnitPlatform()
    forkEvery = 1
    maxParallelForks = 1

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "-----------------------------------------------------"
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            println "-----------------------------------------------------"
        }
    }
}
