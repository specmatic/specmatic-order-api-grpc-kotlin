import org.gradle.plugins.ide.eclipse.model.SourceFolder

buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.5.10"
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'org.springframework.boot' version "3.5.10"
    id 'com.google.protobuf' version "0.9.6"
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.0'
}

group = 'com.store'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:1.63.0"
    implementation "io.grpc:grpc-stub:1.63.0"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation "net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE"
    implementation "build.buf:protovalidate:0.2.1"

    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "io.specmatic.enterprise:executable:${specmaticEnterpriseVersion}"
    testImplementation "org.testcontainers:junit-jupiter:1.21.4"
}

sourceSets {
    main {
        proto {
            srcDir  'specmatic-order-contracts/io/specmatic/examples/store/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.8"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.63.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

eclipse {
    classpath {
        file.beforeMerged { cp ->
            def generatedGrpcFolder = new SourceFolder('src/generated/main/grpc', null)
            generatedGrpcFolder.entryAttributes['ignore_optional_problems'] = 'true'
            cp.entries.add(generatedGrpcFolder)
            def generatedJavaFolder = new SourceFolder('src/generated/main/java', null)
            generatedJavaFolder.entryAttributes['ignore_optional_problems'] = 'true'
            cp.entries.add(generatedJavaFolder)
        }
    }
}

idea {
    module {
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/grpc")
        generatedSourceDirs += file("src/generated/main/java")
        generatedSourceDirs += file("src/generated/main/grpc")
    }
}

test {
    useJUnitPlatform()
}
